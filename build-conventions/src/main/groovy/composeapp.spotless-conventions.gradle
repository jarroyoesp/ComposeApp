plugins {
    id 'com.diffplug.spotless'
}

spotless {
    kotlin {
        target '**/*.kt'
        targetExclude("$buildDir/**/*.kt")
        diktat(libs.versions.diktat.get()).configFile("$rootDir/config/diktat/diktat-analysis.yml")
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    groovyGradle {
        target '**/*.gradle'
        greclipse(libs.versions.greclipse.get()).configFile("$rootDir/config/greclipse/greclipse.properties")
    }

    //format 'graphql', {
    //    target '**/*.graphql'
    //    prettier(libs.versions.prettier.get()).configFile("$rootDir/config/prettier/prettierrc-graphql.yml")
    //}

    format 'yml', {
        target '**/*.yml', '**/*.yaml'
        prettier(libs.versions.prettier.get()).configFile("$rootDir/config/prettier/prettierrc-yml.yml")
    }

    format 'androidXml', {
        target '**/AndroidManifest.xml', 'src/**/*.xml'
        targetExclude '**/mergedManifests/**/AndroidManifest.xml', "**/build/**/*.xml"
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'misc', {
        // define the files to apply `misc` to
        target '**/*.md', '**/.gitignore'

        // define the steps to apply to those files
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.matching { it.name.startsWith('spotless') }.all { mustRunAfter(":app:copyMergedManifests") }

